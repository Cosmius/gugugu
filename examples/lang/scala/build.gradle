plugins {
  id "scala"
}

def scalaVersion = "2.12"
def http4sVersion = "0.20.6"
def log4CatsVersion = "0.3.0"
def logbackVersion = "1.2.3"

repositories {
  jcenter()
}

configurations {
  scalaCompilerPlugin
}

dependencies {
  implementation "org.scala-lang:scala-library:${scalaVersion}.8"

  implementation "org.typelevel:cats-core_${scalaVersion}:1.6.1"
  implementation "org.typelevel:cats-effect_${scalaVersion}:1.3.1"

  implementation "io.circe:circe-core_${scalaVersion}:0.11.1"

  implementation "org.http4s:http4s-core_${scalaVersion}:${http4sVersion}"
  implementation "org.http4s:http4s-client_${scalaVersion}:${http4sVersion}"
  implementation "org.http4s:http4s-server_${scalaVersion}:${http4sVersion}"
  implementation "org.http4s:http4s-circe_${scalaVersion}:${http4sVersion}"
  implementation "org.http4s:http4s-blaze-client_${scalaVersion}:${http4sVersion}"
  implementation "org.http4s:http4s-blaze-server_${scalaVersion}:${http4sVersion}"

  implementation "io.chrisdavenport:log4cats-core_${scalaVersion}:${log4CatsVersion}"
  implementation "io.chrisdavenport:log4cats-slf4j_${scalaVersion}:${log4CatsVersion}"

  implementation "ch.qos.logback:logback-core:${logbackVersion}"
  implementation "ch.qos.logback:logback-classic:${logbackVersion}"

  scalaCompilerPlugin "org.typelevel:kind-projector_${scalaVersion}:0.10.3"
}

sourceSets {
  main {
    scala {
      srcDir "${projectDir}/build/generated/gugugu/main/scala"
    }
  }
}

compileScala {
  scalaCompileOptions.deprecation = true
  scalaCompileOptions.additionalParameters = [
    "-Ypartial-unification",
    "-feature",
    "-Xplugin:" + configurations.scalaCompilerPlugin.asPath]
}


task runJsonHttpServer(type: JavaExec) {
  main = "guguguexamples.jsonhttp.server.JsonHttpServerMain"
}

task runJsonHttpClient(type: JavaExec) {
  main = "guguguexamples.jsonhttp.client.JsonHttpClientMain"
}


tasks.withType(JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  standardInput = System.in
}
