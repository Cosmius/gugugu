plugins {
  id "scala"
}

def scalaVersion = "2.13"
def http4sVersion = "0.23.25"
def http4sBlazeVersion = "0.23.16"
def log4CatsVersion = "2.6.0"
def logbackVersion = "1.4.14"

repositories {
  mavenCentral()
}

configurations {
  scalaCompilerPlugin
}

dependencies {
  implementation "org.scala-lang:scala-library:${scalaVersion}.12"

  implementation "org.typelevel:cats-core_${scalaVersion}:2.10.0"
  implementation "org.typelevel:cats-effect_${scalaVersion}:3.5.3"

  implementation "io.circe:circe-core_${scalaVersion}:0.14.6"

  implementation "org.http4s:http4s-core_${scalaVersion}:${http4sVersion}"
  implementation "org.http4s:http4s-client_${scalaVersion}:${http4sVersion}"
  implementation "org.http4s:http4s-server_${scalaVersion}:${http4sVersion}"
  implementation "org.http4s:http4s-circe_${scalaVersion}:${http4sVersion}"
  implementation "org.http4s:http4s-blaze-client_${scalaVersion}:${http4sBlazeVersion}"
  implementation "org.http4s:http4s-blaze-server_${scalaVersion}:${http4sBlazeVersion}"

  implementation "org.typelevel:log4cats-core_${scalaVersion}:${log4CatsVersion}"
  implementation "org.typelevel:log4cats-slf4j_${scalaVersion}:${log4CatsVersion}"

  implementation "ch.qos.logback:logback-core:${logbackVersion}"
  implementation "ch.qos.logback:logback-classic:${logbackVersion}"

  scalaCompilerPlugin "org.typelevel:kind-projector_2.13.12:0.13.2"
}

sourceSets {
  main {
    scala {
      srcDir "${projectDir}/build/generated/gugugu/main/scala"
    }
  }
}

 compileScala {
   scalaCompileOptions.deprecation = true
   scalaCompileOptions.additionalParameters = [
     "-feature",
     "-Xplugin:" + configurations.scalaCompilerPlugin.asPath]
 }


tasks.register('runJsonHttpServer', JavaExec) {
  mainClass = "guguguexamples.jsonhttp.server.JsonHttpServerMain"
}

tasks.register('runJsonHttpClient', JavaExec) {
  mainClass = "guguguexamples.jsonhttp.client.JsonHttpClientMain"
}


tasks.withType(JavaExec).configureEach {
  classpath = sourceSets.main.runtimeClasspath
  standardInput = System.in
}
